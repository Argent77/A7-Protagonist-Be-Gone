// *** BG2: Modified opening cutscene, part 1 (for party size > 1) ***
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("CSJon")
    CreateCreature("M05PCSPY",[3374.3068],0)
    CreateCreature("M05PCSPY",[3742.2800],0)
    MoveViewPoint([3566.2928],INSTANT)
    Wait(3)
    MultiPlayerSync()
    MoveToPoint([3651.2893])
    MoveToPoint([3666.2878])
    MultiPlayerSync()
    SmallWait(1)
    Wait(1)
    SmallWait(7)
    ActionOverride(Player1,FaceObject("CSJon"))  // Mage
    DisplayStringWait(Myself,5388)  // Ahhh, the child of Bhaal has awoken.
    SmallWait(7)
    DisplayStringHead(Myself,5416)  // It is time for more experiments.
    Wait(3)
    SmallWait(3)
    CreateVisualEffectObject("ICRMPARI",Myself)
    ForceSpell(Player1,CUTSCENE_DAMAGE_1)
    PlaySound("EFF_M20")
    ActionOverride(Player1,LeaveParty())
    ActionOverride(Player1,Kill(Myself))
    ActionOverride(Player1,LeaveParty())
    Wait(2)
    DisplayStringHead(Myself,@1000) // What a disappointment you turned out to be. Oh well, I can still make use of Imoen.
    Wait(5)
    CreateCreature("CSGOLEM",[3240.3304],10)  // Golem
    ActionOverride("csgolem",MoveToPoint([3732.2958]))
    ActionOverride("csgolem",MultiPlayerSync())
    ActionOverride("csgolem",Face(6))
    ActionOverride("csgolem",SmallWait(1))
    ActionOverride("csgolem",DisplayStringHead(Myself,2820))  // More intruders have entered the complex, master.
    Wait(6)
    Face(9)
    Face(12)
    Face(13)
    Face(14)
    Wait(2)
    DisplayStringWait(Myself,2833)  // They act sooner than we had anticipated.
    SmallWait(10)
    DisplayStringWait(Myself,2834)  // No matter, they will only prove a slight delay.
    Wait(1)
    ActionOverride("csgolem",MoveToPoint([3349.3220]))
    ActionOverride("csgolem",DestroySelf())
    Wait(3)
    ForceSpellPoint([3036.3443],WIZARD_DIMENSION_DOOR)
    Wait(2)
    FadeToColor([30.0],0)
    Wait(3)
    FadeFromColor([30.0],0)
    CreateCreature("ISHADDUD",[3269.3264],2)  // Assassin
    CreateVisualEffect("ICSPAIN",[3510.3100])
    PlaySound("EFF_M38")
    SmallWait(7)
    ActionOverride("ishaddud",MoveToPoint([3758.2946]))
    ActionOverride("ishaddud",SmallWait(8))
    ActionOverride("ishaddud",Face(2))
    ActionOverride("ishaddud",SmallWait(8))
    ActionOverride("ishaddud",DisplayStringHead(Myself,54407))  // AHHHHHHHHHH!
    ActionOverride("ishaddud",PlaySound("TUTHH04"))
    ForceSpellPoint([3510.3100],FLASHY_4)
    PlaySound("EFF_M36B")
    SmallWait(7)
    CreateVisualEffect("SPFEARE2",[3413.3178])
    SmallWait(7)
    PlaySound("EFF_M18E")
    ForceSpell("ishaddud",WIZARD_DISINTEGRATE2_IGNORE_RESISTANCE)  // Assassin
    MultiPlayerSync()
    CreateVisualEffectObject("ICFIRSDI","ishaddud")  // Assassin
    PlaySound("EFF_M20")
    Wait(2)
    CreateVisualEffectObject("SPFIREPI","ishaddud")  // Assassin
    PlaySound("EFF_M13")
    ScreenShake([30.30],20)
    Wait(1)
    PlaySound("EFF_P18")
    Wait(3)
    StartCutScene("a7~new2")
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId(Player2)
    Face(2)
    Wait(2)
    SmallWait(7)
    Face(1)
    Face(0)
    Wait(2)
    Face(15)
    Face(14)
    Wait(1)
    SmallWait(7)
    Face(12)
    Face(11)
    Face(10)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId(Player3)
    Face(2)
    Wait(2)
    Face(1)
    Face(0)
    Wait(2)
    Face(15)
    Face(14)
    Wait(2)
    Face(12)
    Face(11)
    Face(10)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId(Player4)
    Wait(2)
    Face(15)
    Face(14)
    Wait(2)
    Face(12)
    Face(11)
    Face(10)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId("%imoen%")
    JumpToPoint([0.0])
    Wait(20)
    %imoen_joined_global%       // SetGlobal("bd_imoen_joined","AR0602",1)
    JoinParty()
END
